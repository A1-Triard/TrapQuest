Player Title by Miscellaneous Frontend begins here.


Include Player Name by Miscellaneous Frontend.

[!<SayTitleBimbo>+

REQUIRES COMMENTING

+!]
To say TitleBimbo:
	if diaper quest is 1:
		say TitleBaby;
	otherwise:
		let T be the class of the player;
		let H be HairColourVagueText;
		say "[NameBimbo] the";
		if the number of worn cursed clothing > 4, say " unlucky";
		if there are worn cursed fake nails, say " delicate";
		if the bimbo of the player < 4:
			say " [one of]determined[or]level headed[or]sensible[sticky random]";
		otherwise if the bimbo of the player < 7:
			say " [if submissive sex addict is 0][one of]confused[or]uneasy[or]curious[sticky random][otherwise]naturally submissive[end if]";
		otherwise if the bimbo of the player < 10:
			say " [one of]flirty[or]perky[or][if the intelligence of the player < 6]air-headed[otherwise]carefree[end if][or]naughty[sticky random]";
		if the player is incontinent:
			if the player is diapered:
				if a random worn diaper is cursed, say " permanently";
				if the incontinence of the player < 8 or diaper lover < 4, say " diaper bound";
				if the incontinence of the player > 7 and diaper lover >= 4, say "poopypantsed";[Better term?]
			otherwise:
				say " incontinent";
		otherwise if the player is diapered:
			say " diapered";
			if incontinence > 2, say " [if there is worn bed wetting clothing]bedwetting[otherwise]accident-prone[end if]";
		let B be 8;
		if extreme proportions fetish is 1, now B is 13;
		if the largeness of breasts > B:
			if the silicone volume of breasts > 10:
				if the silicone volume of breasts > 18 or there are worn fake nails, say " ultra plastic";
				otherwise say " surgically enhanced";
			otherwise:
				if the bimbo of the player < 10, say " [one of][if the player is female]unfortunately endowed[otherwise]ample bosomed[end if][or]buxom[or]big breasted[sticky random]";
				otherwise say " [one of][if the player is female]impressively endowed[otherwise]extremely top heavy[end if][or]famously big breasted[sticky random]";
				if the thickness of hips > 8, say " and";
		if the flesh volume of arms > 9:
			say " obese";
		otherwise if the flesh volume of arms > 7:
			say " overweight";
		otherwise if the flesh volume of arms > 5:
			say " fat";
		otherwise if the flesh volume of arms > 3:
			say " chubby";
		otherwise if the thickness of hips > 8:
			say " [one of][if the flesh volume of hips > 8]huge assed[otherwise]super curvy[end if][or]voluptuous[sticky random]";
		if there is a worn chastity cage:
			if T is not virgin warrior, say " [if the player is male and the size of penis > 6]emasculated[otherwise][one of]locked away[or]chastised[or]caged[sticky random][end if]";
		if the pregnancy of the player > 0:
			if the pregnancy of the player is 2:
				say " permanently pregnant";
			otherwise if the children of the player > 0:
				say " bred";
			otherwise:
				say " pregnant";
		otherwise if the children of the player > 3:
			say " nurturing";
		otherwise if the children of the player > 1:
			say " motherly";
		if the flat intelligence of the player > 19:
			say " [if the flat intelligence of the player > 23]super [end if]genius";
		otherwise if the largeness of hair > 15:
			say " Rapunzel-like";
		otherwise if H is "pink" and the largeness of hair > 9:
			say " long pink haired";
		otherwise if H is "pink":
			say " pink haired";
		otherwise if the largeness of hair > 9:
			say " long haired";
		if the virgin of the player is 1 and T is not virgin warrior:
			say " virgin";
		otherwise if the virgin of the player is 1:
			say " chaste";
		otherwise if the virgin bonus of the player <= -1:
			if the children of the player is 0 and pregnancy fetish is 1 and the pregnancy of the player > 0, say " unwed";
			if the bimbo of the player < 9:
				say " shamefully deflowered";
			otherwise if the bimbo of the player < 14:
				say " broken in";
			otherwise:
				say " soulless";
		if the intelligence of the player < 3 and the raw intelligence of the player < 5:
			say " brainless[if the bimbo of the player > 13] bimbo[end if]";
		if the player is a butt slut:
			if the player-class is not broodmother:
				say " butt slut";
			otherwise:
				say "abdomen slut";
		otherwise if the player is a pussy slut:
			say " craving cunt";
		otherwise if the player is a blowjob slut:
			say " orally fixated";
		otherwise if the player is a tit slut:
			say " fuckpillowed";
		otherwise if there is a worn tongue piercing:
			say " cock [if the oral sex addiction of the player < 6]swallowing[otherwise]hungry[end if]";
		otherwise if the intelligence of the player < 7 and H is "blonde":
			say " dumb blonde";
		otherwise if the bimbo of the player < 13 and the bimbo of the player > 10:
			say " [one of]bimbo[or][if the openness of asshole > 5]anal [end if]slut[or][if the openness of asshole > 5]anal [end if]whore[sticky random]";
		otherwise if the bimbo of the player < 18 and the bimbo of the player >= 13:
			if the sex addiction of the player > 14:
				say " [one of]nymphomaniac[or][if the openness of asshole > 5]anal [end if]orgasm addict[or][if the openness of asshole > 5]anal [end if]sex addict[sticky random]";
			otherwise if the semen taste addiction of the player > 12:
				if the semen taste addiction of the player < 16, say " cum drinker";
				otherwise say " [one of]semen addict[or]cock sucker[sticky random]";
			otherwise:
				say " [one of]bimbo slut[or]dick lover[sticky random]";
		otherwise if the bimbo of the player > 17:
			say "";
		say "[run paragraph on]";
		if the player is flying:
			say " balloon";
		if the humiliation of the player >= 40000 or the bimbo of the player > 17:
			say " [if the sex addiction of the player >= the semen addiction of the player][one of]collection of meat-holes[or]walking selection of fuckholes[or]cock sleeve[or]fuck toy[sticky random][otherwise][one of]cum dumpster[or]semen receptacle[or]human vacuum[sticky random][end if]";
		otherwise if the player is queen of spades and the player is female and the bimbo of the player > 7:
			say " queen of spades";
		if the player is a sissy, say " sissy";
		if T is not "adventurer":
			if T is "catgirl" and the player is male and the bimbo of the player < 13 and the largeness of breasts < 6:
				say " catboy";
			otherwise if T is "puppygirl" and the player is male and the bimbo of the player < 13 and the largeness of breasts < 6:
				say " puppyboy";
			otherwise if the player is a sissy and the player is queen of spades and the bimbo of the player > 7:
				say " black cock [if the sex addiction of the player > 14]addict[otherwise if the sex addiction of the player > 11]lover[otherwise]enthusiast[end if]";
			otherwise:
				say " [T]";
		otherwise if there is a worn leotard and there is a worn ballet shoes or there is a worn ballet heels:
			say " [if the player is male and the bimbo of the player < 9]ballet dancer[otherwise]ballerina[end if]";
		otherwise if there is a worn whip of domination and the delicateness of the player < 10:
			say " dominatrix";
		otherwise if there is an alive newborn giant wasp:
			say " wasp [if the number of alive newborn giant wasps is 1]queen[otherwise]broodmother[end if]";
		otherwise if the virgin bonus of the player <= -2:
			say " [one of]fallen[or]disgraced[sticky random] ex-warrior";
		otherwise if there is a worn T-shirt and the bimbo of the player > 8:
			if there is a worn gang bang girl T-shirt, say " gang bang girl";
			if there is a worn gender bender T-shirt, say " gender bender";
		otherwise if the bimbo of the player < 11:
			if the bimbo of the player < 5 or submissive sex addict is 1:
				say " adventurer";
			otherwise if the delicateness of the player > 12 and diaper lover is 0:
				say " delicate flower";
			otherwise if the player is female and the virgin of the player is 0 and the analvirgin of the player is 1 and trap fetish is 0:
				say " anal virgin";
			otherwise if the size of penis > 7:
				say " [one of]shemale[or]dickgirl[sticky random]";
			otherwise:
				if the delicateness of the player > 11 and diaper lover >= 1:
					say " babygirl";
				otherwise if the size of penis > 4 or the player is a flatchested trap:
					if the number of worn clothing > 4, say " cross-dressing";
					say " trap";
				otherwise:
					say " girl[if the player is male]yboy[end if]";
		if the player is a piss slut and T is not "human toilet":
			say " urinal".

[!<SayTitleBaby>+

REQUIRES COMMENTING

+!]
To say TitleBaby:
	let T be the class of the player;
	let H be HairColourVagueText;
	say "[NameBimbo] the";
	if the player is incontinent:
		if the player is diapered:
			if a random worn diaper is cursed, say " permanently";
			if the incontinence of the player < 8 or diaper lover < 4, say " diaper bound";
			if the incontinence of the player > 7 and diaper lover >= 4, say "poopypantsed";[Better term?]
		otherwise:
			say " incontinent";
	otherwise if the player is diapered:
		say " diapered";
		if incontinence > 2, say " [if there is worn bed wetting clothing]bedwetting[otherwise]accident-prone[end if]";
	if the number of worn cursed clothing > 4, say " unlucky";
	if T is not "adventurer":
		if T is "", say " adult";
		otherwise say "[if the delicateness of the player > 16] baby[end if] [T]";
	otherwise if there is a worn leotard and there is a worn ballet shoes or there is a worn ballet heels:
		say " [if the player is male and the bimbo of the player < 9]ballet dancer[otherwise]ballerina[end if]";
	otherwise:
		if the bimbo of the player < 5 or submissive sex addict is 1:
			say " adventurer";
		otherwise if the delicateness of the player > 16:
			say " baby";
		otherwise if the delicateness of the player > 11:
			say " babygirl";
		otherwise if the size of penis > 4 or the player is a flatchested trap:
			if the number of worn clothing > 4, say " cross-dressing";
			say " trap";
		otherwise:
			say " girl[if the player is male]yboy[end if]".
	


Player Title ends here.
